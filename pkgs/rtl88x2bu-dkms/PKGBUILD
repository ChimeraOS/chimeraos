# Maintainer: Rin Cat <dev@rincat.ch>

pkgname=rtl88x2bu-dkms-git
_pkgbase=rtl88x2bu
pkgver=5.13.1.r199.bf52727
_pkgver=5.13.1
pkgrel=1
pkgdesc="Kernel module for Realtek rtl88x2bu WiFi chipset"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/RinCat/RTL88x2BU-Linux-Driver"
license=('GPL2')
depends=('dkms' 'bc')
makedepends=('git')
source=("git+https://github.com/RinCat/RTL88x2BU-Linux-Driver.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/RTL88x2BU-Linux-Driver"
    printf '%s.r%s.%s' "${_pkgver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "${srcdir}/RTL88x2BU-Linux-Driver"
    mkdir -p "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
    cp -pr * "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
    install -Dm644 dkms.conf "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"
    sed -e "s/@PKGVER@/${pkgver}/" -i "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"
    mkdir -p "${pkgdir}/usr/lib/modprobe.d/"
    echo "blacklist rtw88_8822bu" > "${pkgdir}/usr/lib/modprobe.d/rtw8822bu.conf"
}
